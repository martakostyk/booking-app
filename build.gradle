plugins {
    id 'java'
    id 'jacoco'
}

group 'pl.mrtk'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    annotationProcessor('org.immutables:value:2.8.8')
    implementation ('org.springframework.boot:spring-boot-starter-web:2.5.5') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-undertow:2.5.5'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:2.5.5'

    implementation 'mysql:mysql-connector-java:8.0.26'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.0.0'

    compileOnly 'org.immutables:value:2.8.8'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes 'Main-Class': 'pl.mrtk.bookingapp.BookingApp'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.1 // to be increased
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

